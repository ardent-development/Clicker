(x,op,calclist,autoexec)
Prgm
©x,op,listname,autoexec
 - x: starting value
 - op: any function of x (may contain :)
 - calclist: list to store data to
 - autoexec: # of iterations to automatically compute
Local x,op,calclist,autoexec,k,err,iter,recur,recur2:0→err:0→iter
main\flib("slmsg:Starting Clicker 3.2")

ClrIO

op&"→x"→op

If calclist=0 or calclist="" Then
 Goto fastloop
EndIf

Try
 {}→#calclist
 x→k
 expr(op)
 augment(#calclist,{x})→#calclist
 k→x
Else
 ClrErr
 main\flib("slmsg:Saving to variable sequence due to type")
 DelVar #calclist
 k→x
 Goto seqloop
EndTry


DelVar #calclist
{}→#calclist

If autoexec≠0 Then
 Disp "Computing "&string(autoexec)&" iterations "
 For recur,1,autoexec,1
  Disp "Iteration "&string(recur)&" of "&string(autoexec)

  expr(op)
  augment(#calclist,{x})→#calclist
 EndFor
 Disp "Done."
 Goto quit
EndIf

main\flib("busy:2")
Disp "Ready. (Saving to list)"

While true
 getKey()→k

 ©↓ Enter
 If k=13 Then
  main\flib("busy:1")
  expr(op)
  augment(#calclist,{x})→#calclist
  Disp x
  main\flib("busy:2")

  iter+1→iter
  main\flib("slmsg:Iteration = "&string(iter))

 ©↓ Clear\Home
 ElseIf k=263 or k=277 Then
  Exit
 EndIf
EndWhile
Goto quit


Lbl fastloop

If autoexec≠0 Then
 Disp "Computing "&string(autoexec)&" iterations "
 For recur,1,autoexec,1
  Disp "Iteration "&string(recur)&" of "&string(autoexec)

  expr(op)
 EndFor
 x→lastclic

 Disp "Done."
 Goto quit
EndIf

main\flib("busy:2")
Disp "Ready. (Only saving last)"

While true
 getKey()→k

 ©↓ Enter
 If k=13 Then
  main\flib("busy:1")
  expr(op)
  Disp x
  main\flib("busy:2")

 ©↓ Clear\Home
 ElseIf k=263 or k=277 Then
  x→lastclic
  Exit
 EndIf
EndWhile
Goto quit


Lbl seqloop
Local oldfold,suffix

Try
 getFold()→oldfold
 NewFold clkrvars
Else
 ClrErr
 setFold(clkrvars)→oldfold
EndTry

""→suffix

If autoexec≠0 Then
 Disp "Computing "&string(autoexec)&" iterations "
 For recur,1,autoexec,1
  Disp "Iteration "&string(recur)&" of "&string(autoexec)
  setFold(#oldfold)

  expr(op)
  setFold(clkrvars)
  For recur2,1,4-dim(string(recur))
   "0"&suffix→suffix
  EndFor
  x→#(mid(calclist,1,4)&suffix&string(recur))

  ""→suffix
 EndFor
 DelVar fl
 setFold(#oldfold)

 Disp "Done."
 Goto quit
EndIf

main\flib("busy:2")
Disp "Ready. (Saving to vars)"

While true
 getKey()→k

 ©↓ Enter
 If k=13 Then
  main\flib("busy:1")
  setFold(#oldfold)
  iter+1→iter

  expr(op)
  setFold(clkrvars)
  For recur,1,4-dim(string(iter))
   "0"&suffix→suffix
  EndFor
  x→#(mid(calclist,1,4)&suffix&string(iter))
  Disp x
  ""→suffix

  main\flib("busy:2")
  main\flib("slmsg:Iteration = "&string(iter))

 ©↓ Clear\Home
 ElseIf k=263 or k=277 Then
  Exit
 EndIf
EndWhile
DelVar fl
setFold(#oldfold)


Lbl quit

DispHome
main\flib("busy:3")

If err=0 Then
 main\flib("slmsg:Done! Thanks for using Clicker 3.2")
ElseIf err=1 Then
 main\flib("slmsg:Aborted Clicker 3.2: bad list name")
ElseIf err=2 Then
 main\flib("slmsg:Aborted Clicker 3.2: refusal to continue")
EndIf

DelVar fl
EndPrgm